app TodoApp {
  wasp: {
    version: "^0.13.0"
  },
  title: "TodoApp",
  client: {
    rootComponent: import { Layout } from "@src/Layout.jsx",
  },
  db: {
    prisma: {
      clientPreviewFeatures: ["extendedWhereUnique"]
    }
  },
  auth: {
    userEntity: User,
    methods: {
      usernameAndPassword: {}
    },
    onAuthFailedRedirectTo: "/login",
    onAuthSucceededRedirectTo: "/"
  }
}

route LoginRoute { path: "/login", to: LoginPage }
page LoginPage {
  component: import Login from "@src/pages/auth/Login.jsx"
}
route SignupRoute { path: "/signup", to: SignupPage }
page SignupPage {
  component: import Signup from "@src/pages/auth/Signup.jsx"
}

entity User {=psl
    id    Int    @id @default(autoincrement())
    tasks Task[]
psl=}

entity Task {=psl
    id          Int     @id @default(autoincrement())
    description String
    isDone      Boolean @default(false)
    user        User    @relation(fields: [userId], references: [id])
    userId      Int
psl=}

action createTask {
  fn: import { createTask } from "@src/actions.js",
  entities: [Task]
}

action toggleTask {
  fn: import { toggleTask } from "@src/actions.js",
  entities: [Task]
}

action editTask {
  fn: import { editTask } from "@src/actions.js",
  entities: [Task]
}

query getTasks {
  fn: import { getTasks } from "@src/queries.js",
  entities: [Task]
}

query getUser {
  fn: import { getUser } from "@src/queries.js",
  entities: [User]
}

route MainRoute { path: "/", to: MainPage }
page MainPage {
  component: import MainPage from "@src/pages/Main.jsx",
  authRequired: true
}